plugins {
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '1.14'

    id "org.springframework.boot" version "2.1.1.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "org.asciidoctor.convert" version "1.5.9.2"
}

group = 'io.github.shopping'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

configurations {
    jsondoclet
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-validation',
            'org.springframework.boot:spring-boot-starter-web')

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.2.RELEASE'
    jsondoclet 'capital.scalable:spring-auto-restdocs-json-doclet:2.0.2'
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc:2.0.2.RELEASE',
            'org.springframework.boot:spring-boot-starter-test',
            'capital.scalable:spring-auto-restdocs-core:2.0.2')
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
    options.memberLevel = JavadocMemberLevel.PACKAGE
}

test {
    outputs.dir snippetsDir
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir

    dependsOn jsonDoclet
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
}

lombok {
    version = '1.18.4'
    sha256 = ""
}
